// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {

    ext {
        def coroutines_version = '1.3.9'
        def okhttp_version = '4.9.0'
        def room_version = '2.2.5'
        def startup_version = '1.0.0'

        def mockk_version = '1.10.0'
        def turbine_version = '0.2.1'

        deps = [
                'kotlinxCoroutinesCore'   : "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version",
                'kotlinxCoroutinesAndroid': "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version",
                'okhttp'                  : "com.squareup.okhttp3:okhttp:$okhttp_version",
                'roomRuntime'             : "androidx.room:room-runtime:$room_version",
                'roomKtx'                 : "androidx.room:room-ktx:$room_version",
                'roomCompiler'            : "androidx.room:room-compiler:$room_version",
                'startup'                 : "androidx.startup:startup-runtime:$startup_version"
        ]
        testDeps = [
                'mockk'  : "io.mockk:mockk:$mockk_version",
                'turbine': "app.cash.turbine:turbine:$turbine_version"
        ]

        kotlin_version = "1.5.0"
    }

    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:4.1.3"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.github.dcendents:android-maven-gradle-plugin:2.1'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

subprojects { project ->
    if (project.name == 'annotation' || project.name == 'processor' || project.name == 'kotlin') {
        apply plugin: 'java'
        apply plugin: 'maven'

        sourceCompatibility = 1.8 // java 8
        targetCompatibility = 1.8

        task sourcesJar(type: Jar, dependsOn: classes) {
            classifier = 'sources'
            from sourceSets.main.allSource
        }

        artifacts {
            archives sourcesJar
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}